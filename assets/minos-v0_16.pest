file    =  { SOI ~ "syntax" ~ "=" ~ version ~ ";" ~ (resource | attributed_resource)+ ~ EOI }
version = @{ ASCII_DIGIT+ ~ ("." ~ ASCII_DIGIT+)? }

resource            = {
    "resource" ~ identifier ~ "{" ~ ((default_env | named_env)+ | implicit_default_env) ~ "}"
}
attributed_resource = {
    "resource" ~ identifier ~ "{" ~ ("id" ~ "=" ~ string ~ ";")? ~ (default_env ~ named_env+ | named_env+ | implicit_default_env) ~ "}"
}

named_env            = { "env" ~ identifier ~ "{" ~ policy+ ~ "}" }
default_env          = { "env" ~ "DEFAULT" ~ "{" ~ policy+ ~ "}" }
implicit_default_env = { policy+ }

policy = { "policy" ~ "{" ~ allow ~ ";" ~ rule+ ~ "}" }

allow = { "allow" ~ "=" ~ array }

rule = { "rule" ~ "{" ~ (requirement)+ ~ "}" }

array = { "[" ~ string ~ ("," ~ string)* ~ "]" }

requirement = { (assertion | negation | search) ~ ";" }

assertion = {
    (actor_type ~ assertion_operator ~ resource_type)
  | (resource_type ~ assertion_operator ~ actor_type)
  | (actor_id ~ assertion_operator ~ (resource_id | resource_owner))
  | ((resource_id | resource_owner) ~ assertion_operator ~ actor_id)
  | (actor_type ~ assertion_operator ~ identifier)
  | ((actor_id | resource_id | resource_owner) ~ assertion_operator ~ string)
  | ((actor_groups | actor_roles) ~ assertion_operator ~ array)
}
negation  = {
    (actor_type ~ negation_operator ~ resource_type)
  | (resource_type ~ negation_operator ~ actor_type)
  | (actor_id ~ negation_operator ~ (resource_id | resource_owner))
  | ((resource_id | resource_owner) ~ negation_operator ~ actor_id)
  | (actor_type ~ negation_operator ~ identifier)
  | ((actor_id | resource_id | resource_owner) ~ negation_operator ~ string)
  | ((actor_groups | actor_roles) ~ negation_operator ~ array)
}
search    = { (actor_roles | actor_groups) ~ search_operator ~ (array | string | resource_id | resource_type | resource_owner) }

actor_id     = { "actor.id" }
actor_type   = { "actor.type" }
actor_groups = { "actor.groups" }
actor_roles  = { "actor.roles" }

resource_id    = { "resource.id" }
resource_type  = { "resource.type" }
resource_owner = { "resource.owner" }

assertion_operator = { "=" }
negation_operator  = { "!=" }
search_operator    = { "*=" }

COMMENT = _{ "/*" ~ (!"*/" ~ ANY)* ~ "*/" }

identifier = @{
    (ASCII_ALPHA_UPPER | ASCII_ALPHA_LOWER) ~ (ASCII_ALPHA_LOWER | ASCII_ALPHA_UPPER | ASCII_DIGIT | "_" | "/" | "-")*
}

string       =  { "\"" ~ inner_string ~ "\"" }
inner_string = @{ char* }
char         =  {
    !("\"" | "\\") ~ ANY
  | "\\" ~ ("\"" | "\\" | "/" | "b" | "f" | "n" | "r" | "t")
  | "\\" ~ ("u" ~ ASCII_HEX_DIGIT{4})
}
WHITESPACE   = _{ " " | "\t" | "\r" | "\n" }
