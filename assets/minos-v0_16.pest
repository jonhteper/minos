file    =  { SOI ~ "syntax" ~ "=" ~ version ~ ";" ~ (resource | attributed_resource)+ ~ EOI }
version = @{ ASCII_DIGIT+ ~ ("." ~ ASCII_DIGIT+)? }

resource            = {
    "resource" ~ identifier ~ "{" ~ ((default_env | named_env)+ | implicit_default_env) ~ "}"
}
attributed_resource = {
    "resource" ~ identifier ~ "{" ~ ("id" ~ "=" ~ string ~ ";")? ~ (default_env ~ named_env+ | named_env+ | implicit_default_env) ~ "}"
}

named_env            = { "env" ~ identifier ~ "{" ~ policy+ ~ "}" }
default_env          = { "env" ~ "DEFAULT" ~ "{" ~ policy+ ~ "}" }
implicit_default_env = { policy+ }

policy = { "policy" ~ "{" ~ allow ~ ";" ~ rule+ ~ "}" }

allow = { "allow" ~ "=" ~ array }

rule = { "rule" ~ "{" ~ (requirement)+ ~ "}" }

array = { "[" ~ string ~ ("," ~ string)* ~ "]" }

requirement = { (assertion | negation | search) ~ ";" }

assertion = {
    ("actor.type" ~ "=" ~ "resource.type")
  | ("resource.type" ~ "=" ~ "actor.type")
  | ("actor.id" ~ "=" ~ ("resource.id" | "resource.owner"))
  | (("resource.id" | "resource.owner") ~ "=" ~ "actor.id")
  | ("actor.type" ~ "=" ~ identifier)
  | (("actor.id" | "resource.id" | "resource.owner") ~ "=" ~ string)
  | (("actor.groups" | "actor.roles") ~ "=" ~ array)
}
negation  = {
    ("actor.type" ~ "!=" ~ "resource.type")
  | ("resource.type" ~ "!=" ~ "actor.type")
  | ("actor.id" ~ "!=" ~ ("resource.id" | "resource.owner"))
  | (("resource.id" | "resource.owner") ~ "!=" ~ "actor.id")
  | ("actor.type" ~ "!=" ~ identifier)
  | (("actor.id" | "resource.id" | "resource.owner") ~ "!=" ~ string)
  | (("actor.groups" | "actor.roles") ~ "!=" ~ array)
}
search    = { ("actor.roles" | "actor.groups") ~ "*=" ~ (array | string) }

COMMENT = _{ "/*" ~ (!"*/" ~ ANY)* ~ "*/" }

identifier = @{
    (ASCII_ALPHA_UPPER | ASCII_ALPHA_LOWER) ~ (ASCII_ALPHA_LOWER | ASCII_ALPHA_UPPER | ASCII_DIGIT | "_" | "/" | "-")*
}

string       =  { "\"" ~ inner_string ~ "\"" }
inner_string = @{ char* }
char         =  {
    !("\"" | "\\") ~ ANY
  | "\\" ~ ("\"" | "\\" | "/" | "b" | "f" | "n" | "r" | "t")
  | "\\" ~ ("u" ~ ASCII_HEX_DIGIT{4})
}
WHITESPACE   = _{ " " | "\t" | "\r" | "\n" }
