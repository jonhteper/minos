file = { SOI ~ "sintaxis" ~ "=" ~ number ~ ";" ~ env+ ~ EOI }

env = { "env" ~ identifier ~ "{" ~ resource+ ~ "}" }

resource = { "resource" ~ identifier ~ "{" ~ ("id" ~ "=" ~ string ~ ";")? ~ policy+ ~ "}" }

rule = { "rule" ~ "{" ~ (actorAttribute)+ ~ "}" }

policy = { "policy" ~ "{" ~ "allow" ~ "=" ~ "[" ~ string ~ ("," ~ string)* ~ "]" ~ ";" ~ rule+ ~ "}" }

actorAttribute = {
    ("actor.type" | "actor.id") ~ ("=" | "!=") ~ identifier ~ ";"
  | ("actor.groups" | "actor.roles") ~ ("=" | "*=") ~ "[" ~ string ~ ("," ~ string)* ~ "]" ~ ";"
}

COMMENT = _{ "/*" ~ (!"*/" ~ ANY)* ~ "*/" }

number = @{ ASCII_DIGIT+ ~ ("." ~ ASCII_DIGIT+)? }

identifier = @{ (ASCII_ALPHA_UPPER | ASCII_ALPHA_LOWER) ~ (ASCII_ALPHA_LOWER | ASCII_ALPHA_UPPER |ASCII_DIGIT | "_" | "/" | "-" )* }

string     = ${ "\"" ~ inner ~ "\"" }
inner      = @{ char* }
char       =  {
    !("\"" | "\\") ~ ANY
  | "\\" ~ ("\"" | "\\" | "/" | "b" | "f" | "n" | "r" | "t")
  | "\\" ~ ("u" ~ ASCII_HEX_DIGIT{4})
}
WHITESPACE = _{ " " | "\t" | "\r" | "\n" }
